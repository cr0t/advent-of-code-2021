# Day 10

## Setup

```elixir
Mix.install([
  {:kino, "~> 0.4.1"}
])
```

## Intro

```elixir
input = Kino.Input.textarea("Please paste your input:")
```

## Parsing/Shared

```elixir
defmodule Day10Shared do
  def parse(input) do
    input
    |> Kino.Input.read()
    |> String.split("\n", trim: true)
    |> Enum.map(&String.codepoints/1)
  end
end

Day10Shared.parse(input)
```

## Part 1

```elixir
defmodule Day10Part1Solution do
  @counterparts %{
    "(" => ")",
    "[" => "]",
    "{" => "}",
    "<" => ">"
  }

  @openers Map.keys(@counterparts)

  @points %{
    ")" => 3,
    "]" => 57,
    "}" => 1197,
    ">" => 25137
  }

  def solve(input) do
    input
    |> Enum.map(&corruptor(&1, []))
    |> Enum.reject(&(&1 == nil))
    |> Enum.map(&@points[&1])
    |> Enum.sum()
  end

  defp corruptor([], []),
    do: nil

  defp corruptor([], _),
    do: nil

  defp corruptor([br | tail], stack) when br in @openers,
    do: corruptor(tail, [@counterparts[br] | stack])

  defp corruptor([br | tail], [br | t_stack]),
    do: corruptor(tail, t_stack)

  defp corruptor([illegal_br | _], [_correct_br | _]),
    do: illegal_br
end

Day10Shared.parse(input)
|> Day10Part1Solution.solve()
```

## Part 2

```elixir
defmodule Day10Part2Solution do
  @counterparts %{
    "(" => ")",
    "[" => "]",
    "{" => "}",
    "<" => ">"
  }

  @openers Map.keys(@counterparts)

  @points %{
    ")" => 1,
    "]" => 2,
    "}" => 3,
    ">" => 4
  }

  def solve(input) do
    points =
      input
      |> Enum.map(&balancer(&1, []))
      |> Enum.reject(&(&1 == nil))
      |> Enum.map(fn balancers ->
        balancers
        |> Enum.map(&@points[&1])
        |> Enum.reduce(0, &(&2 * 5 + &1))
      end)
      |> Enum.sort()

    Enum.at(points, div(length(points), 2))
  end

  defp balancer([], []),
    do: nil

  defp balancer([], balancers),
    do: balancers

  defp balancer([br | tail], stack) when br in @openers,
    do: balancer(tail, [@counterparts[br] | stack])

  defp balancer([br | tail], [br | t_stack]),
    do: balancer(tail, t_stack)

  defp balancer([_illegal_br | _], _balancers),
    do: nil
end

Day10Shared.parse(input)
|> Day10Part2Solution.solve()
```
